apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'groovy'

project.group = 'co.yellowbricks.boxed'
project.archivesBaseName = 'boxed-api'
project.version = "0.0.1.$buildId"

sourceCompatibility = 8
targetCompatibility = 8

buildscript {
    repositories {
        maven {
            url "http://nexus.int.klarna.net/content/groups/public/"
        }
        maven {
            url "https://nexus.int.klarna.net/content/repositories/gradle-plugins-m2-proxy/"
        }
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

repositories {
    maven {
        url "http://nexus.int.klarna.net/content/groups/public/"
    }
}

ext.dropwizardVersion = '1.0.6'
ext.logbackContribVersion = '0.1.2'

mainClassName = 'co.yellowbricks.boxed.BoxedServiceStartup'
run {
    if (System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

dependencies {
    compile(group: 'io.dropwizard', name: 'dropwizard-core', version: "$dropwizardVersion")
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: "$dropwizardVersion"
    compile group: 'io.dropwizard', name: 'dropwizard-migrations', version: "$dropwizardVersion"
    compile(group: 'io.dropwizard', name: 'dropwizard-client', version: "$dropwizardVersion")
    compile group: 'io.dropwizard', name: 'dropwizard-auth', version: "$dropwizardVersion"

    compile group: 'org.postgresql', name: 'postgresql', version: "9.4.1208"
    compile group: 'com.h2database', name: 'h2', version: '1.4.191'
    compile group: 'com.google.inject', name: 'guice', version: "4.1.0"
    compile group: 'ch.qos.logback.contrib', name: 'logback-json-core', version: "$logbackContribVersion"
    compile group: 'ch.qos.logback.contrib', name: 'logback-json-classic', version: "$logbackContribVersion"
    compile group: 'ch.qos.logback.contrib', name: 'logback-jackson', version: "$logbackContribVersion"

    compile group: 'org.apache.commons', name: 'commons-lang3', version: "3.4"
    compile group: 'org.apache.commons', name: 'commons-io', version: "1.3.2"

    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
    testCompile group: 'cglib', name: 'cglib', version: '3.2.0'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.2'
}

tasks.withType(JavaCompile) {
    options.deprecation = true
    options.encoding = 'UTF-8'
}

jar {
    def manifestClasspath = configurations.runtime.collect { it.getName() }.join(' ')
    manifest {
        attributes 'Build-Date': new Date(),
                   'Build-JDK': System.getProperty('java.version'),
                   'Main-Class': "$mainClassName",
                   'Class-Path': manifestClasspath,
                   'Klarna-Service-Name': "$project.archivesBaseName",
                   'Klarna-Service-Version': "$project.version"
    }
}

task copyJarToDistribution(type: Copy) {
    dependsOn jar
    from jar.archivePath
    into "$distsDir"
}

task copyDependenciesToDistribution(type: Copy) {
    from configurations.runtime
    into "$distsDir"
}

assemble {
    dependsOn copyJarToDistribution, copyDependenciesToDistribution
}

task showVersion() {
    println 'projectVersion=' + project.version
}


test {
    testLogging {
        events "skipped", "failed", "standardError"
    }
}
